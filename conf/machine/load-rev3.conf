#@TYPE: Machine
#@NAME: Programmable Load Controller
#@DESCRIPTION: Machine definition for the Rev 3 programmable load controller

require conf/machine/include/stm32mp1.inc

# build an eMMC image (no SD card slot on this board)
WKS_FILE = "programmable-load-emmc.wks"


# Kernel device tree (our custom kernel recipe installs this)
KERNEL_DEVICETREE = "stm32mp151a-programmable-load-myir-mx.dtb"

# U-Boot board/device tree config
SPL_BINARY = "u-boot-spl.stm32"
UBOOT_MAKE_TARGET = "DEVICE_TREE=stm32mp151a-programmable-load-myir-mx all"
UBOOT_MACHINE = "stm32mp15_basic_defconfig"

# U-Boot extlinux configuration
UBOOT_EXTLINUX = "1"
UBOOT_EXTLINUX_FDT = "/boot/stm32mp151a-programmable-load-myir-mx.dtb"

# boot from eMMC
# NOTE: rootedlay/rootwait are required for slow eMMC devices.
UBOOT_EXTLINUX_ROOT = "root=/dev/mmcblk0p3 rootfstype=erofs rootwait"
UBOOT_EXTLINUX_CONSOLE = ""
# ensure we use our custom overlay mounter as init
UBOOT_EXTLINUX_KERNEL_ARGS = "init=/usr/sbin/init-overlays"

# enable USB gadget support
MACHINE_FEATURES = "usbhost usbgadget"
# required images
MACHINE_ESSENTIAL_EXTRA_RDEPENDS += " \
    kernel-image \
    kernel-devicetree \
    kernel-modules \
    u-boot \
"

# test utility for display controller
IMAGE_INSTALL:append = " spidev-test "
